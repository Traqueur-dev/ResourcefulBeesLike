plugins {
    id 'java'
    id("io.papermc.paperweight.userdev") version "1.7.1"
}

group = 'fr.traqueur.ressourcefulbees'
version = '1.0-SNAPSHOT'

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

dependencies {
    paperweight.paperDevBundle("1.20.4-R0.1-SNAPSHOT")
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.papermc.paperweight.userdev'

    group = 'fr.traqueur.ressourcefulbees'
    version = '0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        maven {
            name = "papermc-repo"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
    }

    dependencies {
        paperweight.paperDevBundle("1.20.4-R0.1-SNAPSHOT")
        compileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    }
}

tasks.register("buildAll") {
    group = "ressourcefulbees"
    description = "Build all RessourcefulBeesLike related plugins"
    dependsOn(
            ":RessourcefulBeesLikeAPI:build",
            ":RessourcefulBeesLikePlugin:build"
    )
}